"""
Public API NodeJS repositories, plus some legacy cruft that `yarn_install` needs.
"""

load("//lib/private:utils/platform.bzl", "PLATFORMS")
load("//lib/private:utils/strings.bzl", "dedent")

visibility(["//lib/private"])

def _nodejs_toolchains_impl(repository_ctx):
    # Base BUILD file for this repository
    repository_ctx.file(
        "BUILD.bazel",
        content = "",
    )

    # Create toolchain declarations
    toolchain_blocks = []
    for platform in PLATFORMS.values():
        # Note no `target_compatible_with`, this is left up to rules (NodeJS is much more generalised than a compiler)
        # `exec_compatible_with` ensures NodeJS is only used where it can run
        toolchain_blocks.append(dedent("""
            toolchain(
                name = "node_{id}_toolchain",
                exec_compatible_with = {constraints},
                toolchain = "@nodejs_{id}_config//:toolchain",
                toolchain_type = "@build_bazel_rules_nodejs//lib:nodejs_toolchain_type",
            )
        """.format(
            id = platform["id"],
            constraints = repr(platform["constraints"]),
        )))

    repository_ctx.file(
        "toolchains/BUILD.bazel",
        content = dedent("""
            # Generated by node_repositories.bzl
            {toolchains}
        """.format(
            toolchains = "\n\n".join(toolchain_blocks),
        )),
    )

nodejs_toolchains = repository_rule(
    _nodejs_toolchains_impl,
    attrs = {"node_version": attr.string()},
)
